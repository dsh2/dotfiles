# TODO
# -Add binding to pin current item to then relax the filter in order to
#  explore the environment
# -Add some means to add an ordering to the selection of --multi
# -Add some means to match content in a particular column

export FZF_DEFAULT_OPTS="
			--bind 'alt-a:select-all'
			--bind 'alt-b:deselect-all'
			--bind 'alt-d:page-down'
			--bind 'alt-u:page-up'
			--bind 'ctrl-r:toggle-sort'
			--bind 'ctrl-q:toggle-preview'
			--bind 'ctrl-alt-j:preview-page-down'
			--bind 'ctrl-alt-k:preview-page-up'
			--bind 'ctrl-alt-l:toggle-preview-wrap'
			--bind 'ctrl-l:cancel'
			--bind 'ctrl-o:toggle-all'
			--bind 'alt-c:toggle-all'
			--bind 'ctrl-n:next-history'
			--bind 'ctrl-p:previous-history'
			--bind 'ctrl-t:execute(tmux set-buffer {})'
			--bind 'ctrl-x:execute(xclip -selection clipboard -in <<< {})'
			--bind 'tab:toggle-up'
			--bind 'btab:toggle-down'
			--ansi
			--inline-info
			--history=${HOME}/.fzf_history
			--exact
			--prompt=\"fzf: \"
"
export FZF_TMUX=1
export FZF_TMUX_HEIGHT=100%

# TODO: add binding for vared
ee() {
    typeset | fzf --multi | cut -d= -f1 
}

fbr() {
    local refs_filter=${*}
    local object
    object=$(git -c color.ui=always for-each-ref \
		${refs_filter} \
		  --format="%(HEAD)%(color:red)%(objectname:short)%(color:reset) %09 (%(color:green)%(committerdate:local) - %(committerdate:relative)%(color:reset)) %09 %(color:yellow)%(refname:short)%(color:reset) %(contents:subject) (%(color:green)%(authorname)%(color:reset))" \
		  --sort=-committerdate |
      fzf-tmux --tabstop=14 --ansi --no-sort | cut -f 1 | tr -d \*)
    echo sha1 = $object
    git checkout $=object
}

fbT() {
    fbr "refs/tags"
}

fbS() {
    fbr "refs/stash"
}

fbR() {
    fbr "refs/heads"
}

# fshow - git commit browser
fshow() {
  git log --graph --color=always \
      --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |
  fzf --ansi --no-sort --reverse --tiebreak=index --bind=ctrl-s:toggle-sort \
      --bind "ctrl-m:execute:
                (grep -o '[a-f0-9]\{7\}' | head -1 |
                xargs -I % sh -c 'git show --color=always % | less -R') << 'FZF-EOF'
                {}
FZF-EOF"
}

# fs - tmux session management
fs() {
  local session
  session=$(tmux list-sessions -F "#{session_name}" | \
    fzf --query="$1" --select-1 --exit-0) &&
  tmux switch-client -t "$session"
}

ftpane() {
  # TODO: check if already running
  panes=$(tmux list-panes -aF '#{session_name}:#{window_index}:#{pane_index}:#{pane_tty}|#{pane_width}x#{pane_height}(#{pane_id})\tname:#{=15:window_name}\t\tcmd:#{=-15:pane_current_command}\t\tdir:#{pane_current_path}')
  current_pane=$(tmux display-message -p '#I:#P')
  current_window=$(tmux display-message -p '#I')

  target=$(echo "$panes" | grep -v "$current_pane" | fzf +m --reverse --tabstop=12 ) || return

  target_session=$(echo $target | awk 'BEGIN{FS=":|-"} {print$1}')
  target_window=$(echo $target | awk 'BEGIN{FS=":|-"} {print$2}')
  target_pane=$(echo $target | awk 'BEGIN{FS=":|-"} {print$3}' | cut -c 1)

  tmux switch-client -t $target_session:$target_window.$target_pane
}

# gch - browse chrome history
gch() {
	local title_width sep tmp_file
	title_width=$(( COLUMNS / 2 ))
	sep='{{::}}'
	tmp_file=$(mktemp)
    cp -f "$HOME/.config/google-chrome-history" $tmp_file &&
		sqlite3 -separator $sep $tmp_file  \
			"select datetime((last_visit_time/1000000)-11644473600, 'unixepoch', 'localtime'), visit_count, substr(title, 1, $title_width), url from urls order by last_visit_time desc" |
		awk -F $sep '{printf "%s #%s %-'$title_width's  \x1b[36m%s\n", $1, $2, $3, $4}' |
		fzf --ansi --multi \
			--prompt "chrome history ($(jq -c '.["account_info"] | .[0] | .email' "$(dirname "$(readlink  ~/.config/google-chrome-history)")"/Preferences)): " |
		sed 's#.*\(https*://\)#\1#' | xargs exo-open > /dev/null 2>&1
	zle && zle redisplay
}
zle -N gch; bindkey '\eU' gch

# gchb - browse chrome bookmarks
gchb() {
	setopt pipefail
	local urls
	urls=$(chrome-bookmarks.rb | sort -r | fzf --ansi --multi | cut -f 3) &&
	    exo-open $=urls
}
zle -N gchb; bindkey '\eB' gchb

__fzfz() {
		unset REPORTTIME
		eval "z -l|cut -f 2" | fzf --prompt='z (dirs): ' --tac --tiebreak=index -m --preview="ls --color=always -al {} " | while read item; do
    printf '%q ' "$item"
  done
  echo
}

fzfz-file-widget() {
  LBUFFER="${LBUFFER}$(__fzfz)"
  zle redisplay
}

zle     -N   fzfz-file-widget
bindkey '^G' fzfz-file-widget

__fzflocate() {
    unset REPORTTIME
    \locate \*  | fzf --prompt='locate: ' --preview="file {}"
}

fzf-locate-widget() {
  LBUFFER="${LBUFFER}$(__fzflocate)"
  zle redisplay
}

zle     -N   fzf-locate-widget
bindkey '^x^l' fzf-locate-widget

# vim:ft=sh
